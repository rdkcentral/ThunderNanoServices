set (startmode ${PLUGIN_BLUETOOTHSDPSERVER_STARTMODE})

function(add_service config)
   set(optionsArgs PUBLIC)
   set(oneValueArgs NAME CALLSIGN DESCRIPTION CLASS)
   set(multiValueArgs PARAMETER)

   cmake_parse_arguments(PARSE_ARGV 1 Argument "${optionsArgs}" "${oneValueArgs}" "${multiValueArgs}")
   if(Argument_UNPARSED_ARGUMENTS)
      message(FATAL_ERROR "Unknown keywords given to add_app(): \"${Argument_UNPARSED_ARGUMENTS}\"")
   endif()

   map()
      kv(name ${Argument_NAME})
      kv(callsign ${Argument_CALLSIGN})
      if(Argument_DESCRIPTION)
         kv(description ${Argument_DESCRIPTION})
      endif()
      if(Argument_PUBLIC)
        kv(public "true")
      endif()
      kv(class ${Argument_CLASS})
      if(Argument_PARAMETER)
         key(parameters)
         map()
            foreach(__entry_str ${Argument_PARAMETER})
               string(REPLACE "=" ";" __entry ${__entry_str})
               list(GET __entry 0 __name)
               list(GET __entry 1 __values_str)
               string(REPLACE "|" ";" __values ${__values_str})
               kv(${__name} ___array___ ${__values})
            endforeach()
         end()
      endif()
   end()
   ans(__service)
   map_append(${config} services ___array___ ${__service})
endfunction(add_service)

map()
   kv(controller ${PLUGIN_BLUETOOTHSDPSERVER_CONTROLLER})
   kv(provider ${PLUGIN_BLUETOOTHSDPSERVER_PROVIDER})
end()
ans(configuration)

# supported classes: "a2dp-audio-sink", "a2dp-audio-source"

# A2DP AUIDO SOURCE SERVICE
# supported audio source features: "player", "microphone", "tuner", "mixer"
if(PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DP_AUDIO_SOURCE)
add_service( ${configuration}
   PUBLIC
   NAME "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DPAUDIOSOURCE_NAME}"
   DESCRIPTION "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DPAUDIOSOURCE_DESCRIPTION}"
   CALLSIGN "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DP_AUDIO_SOURCE}"
   CLASS "a2dp-audio-source"
   PARAMETER "features=player|tuner")
endif()

# A2DP AUIDO SINK SERVICE
if (PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DP_AUDIO_SINK)
# supported audio sink features: "headphone", "speaker", "recorder", "amplifier"
add_service( ${configuration}
   PUBLIC
   NAME "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DPAUDIOSINK_NAME}"
   DESCRIPTION "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DPAUDIOSINK_DESCRIPTION}"
   CALLSIGN "${PLUGIN_BLUETOOTHSDPSERVER_SERVICE_A2DP_AUDIO_SINK}"
   CLASS "a2dp-audio-sink"
   PARAMETER "features=amplifier")
endif()
